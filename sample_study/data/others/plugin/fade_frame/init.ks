; 2019.11.26 ko10panda edit
;*****************************************************************************

;   メッセージウィンドウとシステムボタンのフェード表示＆消去プラグイン

;*****************************************************************************
; 改造はご自由にどうぞ。基本的にノーサポートでよろしくお願いします。
; 使用時はクレジット表記をお願いいたします --> 空想曲線（https://kopacurve.blog.fc2.com/）


;============================================================================
; つかいかた
;============================================================================
; １． [hide_frame]でメッセージウィンドウとシステムボタンを非表示にする
; ２． メッセージウィンドウとシステムボタンの設定をおこなう
; ３．[transparent_frame]でメッセージウィンドウとシステムボタンを可視化＋不透明度０にする
; ４．表示したいときは[fadein_frame]
; ５．消去したいときは[fadeout_frame]

; フェードアウトしても画面上にはメッセージウィンドウもシステムボタンも透明な状態で残っています
; ゲームの状況に応じて[freeimage]や[clearfix]タグで消去してやってください


; ▼マクロ定義
;=============================================================================
; メッセージウィンドウとシステムボタンを非表示に
;=============================================================================
; [hide_frame layer=""]
;
; layerにはメッセージウィンドウに使用しているレイヤを記述します
; デフォルト値は message0 です
;-----------------------------------------------------------------------------
[macro name="hide_frame"]
  [iscript]

  var layer = mp.layer || "message0"
      layer = layer + '_fore';

  $(".layer .message_inner").hide();
  $(".layer .message_outer").hide();
  $(".fixlayer").hide();

  [endscript]
  [endmacro]


;=============================================================================
; メッセージウィンドウとシステムボタンのopacityを0にして可視化
;=============================================================================
; [transparent_frame layer=""]
;
; layerにはメッセージウィンドウに使用しているレイヤを記述します
; デフォルト値は message0 です
;-----------------------------------------------------------------------------
[macro name="transparent_frame"]
  [iscript]

  var layer = mp.layer || "message0"
      layer = layer + '_fore';

  $(".layer .message_inner").css("opacity", 0).show();
  $(".layer .message_outer").css("opacity", 0).show();
  $(".fixlayer").css("opacity", 0).show();

  [endscript]
  [endmacro]


;=============================================================================
; フェードイン
;=============================================================================
; [fadein_frame layer="" time="" opacity="" type=""]
;
; メッセージウィンドウとシステムボタンをフェード表示します
; パラメータは以下のとおりです
;
; layer  ：メッセージウィンドウに使用しているメッセージレイヤ（初期値 message0）
; time   ：フェードにかける時間（ミリ秒・初期値1000）
; opacity：メッセージウィンドウの不透明度（0～255・初期値255）
; type　 ：フェードする対象を指定できます（何も指定しなければ両方ともフェード表示します）
;          sysbtn：システムボタンだけフェード表示
;          message：メッセージウィンドウだけフェード表示

; ※すでに表示されているメッセージウィンドウに対してマクロを使うこともできます
;   メッセージウィンドウの不透明度をゆっくり変えたいときにお使いください
;-----------------------------------------------------------------------------
[macro name="fadein_frame"]

; メッセージウィンドウ
  [if exp="mp.type != 'sysbtn'"]
    [anim layer="%layer|message0"                        time="%time|1000" opacity="%opacity|255"]
    [anim layer="%layer|message0" name="message_outer"   time="%time|1000" opacity="%opacity|255"]
    [anim layer="%layer|message0" name="message_inner"   time="%time|1000" opacity="255"]
    [anim layer="%layer|message0" name="chara_name_area" time="%time|1000" opacity="255"]
    [endif]

; システムボタン
  [if exp="mp.type != 'message'"]
    [anim name="fixlayer" time="%time|1000" opacity="255"]
    [endif]

; フェードを待つ
  [wa]

  [endmacro]


;=============================================================================
; フェードアウト
;=============================================================================
; [fadeout_frame layer="" time="" type=""]
;
; メッセージウィンドウとシステムボタンをフェードアウトします
; パラメータは以下のとおりです
;
; layer  ：メッセージウィンドウに使用しているメッセージレイヤ（初期値 message0）
; time   ：フェードにかける時間（ミリ秒・初期値1000）
; type　 ：フェードする対象を指定できます（何も指定しなければ両方ともフェードアウトします）
;          sysbtn：システムボタンだけフェードアウト
;          message：メッセージウィンドウだけフェードアウト
;-----------------------------------------------------------------------------
[macro name="fadeout_frame"]

; メッセージウィンドウ
  [if exp="mp.type != 'sysbtn'"]
    [iscript]
    $(".chara_name_area").empty();
    [endscript]
    [anim layer="%layer|message0" time="%time|1000" opacity="0"]
    [endif]

; システムボタン
  [if exp="mp.type != 'message'"]
    [anim name="fixlayer" time="%time|1000" opacity="0"]
    [endif]

; フェードを待つ
  [wa]

  [endmacro]



[return]
